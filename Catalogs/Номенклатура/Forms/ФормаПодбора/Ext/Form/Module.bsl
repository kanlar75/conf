&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Склад", Параметры.СкладПараметр);
	Список.Параметры.УстановитьЗначениеПараметра("ВидЦены", Параметры.ВидЦеныПараметр);
	Склад = Параметры.СкладПараметр;
	ВидЦены = Параметры.ВидЦеныПараметр;
	ВыбранныеТовары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВХ));
	
КонецПроцедуры   

&НаСервере
Функция ЦенаРеализацииНоменклатурыНаСервере(Номенклатура)
	
	Возврат ПолучениеДанныхНаСервере.ЦенаНоменклатурыНаСервере(Номенклатура, ВидЦены, ТекущаяДата()); 
	
КонецФункции

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	Список.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦеныПриИзменении(Элемент)
	
	Список.Параметры.УстановитьЗначениеПараметра("ВидЦены", ВидЦены);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеТоварыНоменклатураПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ВыбранныеТовары.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		ТекСтрока.Цена = ЦенаРеализацииНоменклатурыНаСервере(ТекСтрока.Номенклатура);
		ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВыбранныеТовары.ТекущиеДанные;
	ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВыбранныеТовары.ТекущиеДанные;
	ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеТоварыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ПеренестиСписокНомеклатурыВВыбранныеТовары(ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры 

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбранноеЗначение, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		ПеренестиНоменклатуруВВыбранныеТовары(ВыбранноеЗначение);	
	Иначе
		Для каждого Номенклатура Из ВыбранноеЗначение Цикл
			ПеренестиНоменклатуруВВыбранныеТовары(Номенклатура);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиНоменклатуруВВыбранныеТовары(Номенклатура)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	НайденныеСтроки = ВыбранныеТовары.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Если Не Номенклатура.ЭтоГруппа Тогда
			НоваяСтрока = ВыбранныеТовары.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.Цена = ЦенаРеализацииНоменклатурыНаСервере(Номенклатура);	
		КонецЕсли;
	Иначе
		НайденнаяСтрока = НайденныеСтроки[0];
		НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + 1;
		НайденнаяСтрока.Стоимость = НайденнаяСтрока.Цена * НайденнаяСтрока.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиСписокНомеклатурыВВыбранныеТовары(СписокНоменклатуры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокНоменклатуры)
	|	И НЕ Номенклатура.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ПеренестиНоменклатуруВВыбранныеТовары(Выборка.Ссылка);	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьПодборНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(ВыбранныеТовары.Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьПодбор(Команда)
	
	АдресВХ = ЗавершитьПодборНаСервере();
	ВладелецФормы.ОбработатьПодбор(АдресВХ);
	Закрыть();
	
КонецПроцедуры

