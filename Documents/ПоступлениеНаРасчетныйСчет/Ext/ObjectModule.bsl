
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		ТипОснованиеСтрока = СтрЗаменить(ДанныеЗаполнения.Метаданные().ПолноеИмя(), "Документ.", "");
		ТипДокументаСтрока = СтрЗаменить(ЭтотОбъект.Ссылка.Метаданные().ПолноеИмя(), "Документ.", "");
		Документ = ПолучениеДанныхНаСервере.ЕстьДокументВведенныйНаОсновании(ДанныеЗаполнения.Ссылка, ТипОснованиеСтрока, ТипДокументаСтрока);
		Если Документ = Ложь Тогда
			// Заполнение шапки
			Валюта = ДанныеЗаполнения.Валюта;
			Договор = ДанныеЗаполнения.Договор;
			Покупатель = ДанныеЗаполнения.Покупатель;
			Основание = ДанныеЗаполнения.Ссылка;
			СуммаПоДокументу = ДанныеЗаполнения.СуммаПоДокументу;
		Иначе
			ВызватьИсключение("По " + СлужебныеПроцедурыНаСервере.СформироватьПредставлениеДокумента(ДанныеЗаполнения.Ссылка) + 
			" уже существует " + СлужебныеПроцедурыНаСервере.СформироватьПредставлениеДокумента(Документ));
			Возврат; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если Валюта <> Справочники.Валюты.Рубль Тогда
		Курс = ПолучениеДанныхНаСервере.ПолучитьКурс(Валюта, Дата);
		Если НЕ Курс Тогда
			Сообщить("Для валюты " + Валюта + " курс не установлен или равен 0");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	ИначеЕсли Валюта = Справочники.Валюты.Рубль Тогда
		Курс = 1;
	КонецЕсли;
	Если  Валюта <> СчетЗачисления.Валюта  Тогда
		Сообщить ("Валюта документа " + Валюта + ", а зачисление на счет в валюте " + СчетЗачисления.Валюта);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// регистр Хозрасчетный 
	Движения.Хозрасчетный.Записывать = Истина;
	Движение = Движения.Хозрасчетный.Добавить();
	Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
	Движение.Период = Дата;
	Если Валюта = Справочники.Валюты.Рубль Тогда
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетныеСчета;	
	ИначеЕсли Валюта <> Справочники.Валюты.Рубль Тогда
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.ВалютныеСчета;
		Движение.ВалютнаяСуммаДт = СуммаПоДокументу;
		Движение.ВалютаДт = Валюта;
	КонецЕсли;
	Движение.Сумма = СуммаПоДокументу * Курс;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СчетаОрганизации] = СчетЗачисления;
	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Покупатель;
	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
	
КонецПроцедуры

