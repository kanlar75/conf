&НаСервере
Функция ЦенаРеализацииНоменклатурыНаСервере(Номенклатура)
	
	Возврат ПолучениеДанныхНаСервере.ЦенаНоменклатурыНаСервере(Номенклатура, Объект.ВидЦены, Объект.Дата); 
	
КонецФункции

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекСтрока);
	Объект.СуммаПоДокументу = Объект.Товары.Итог("Стоимость");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаРеализацииПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекСтрока);
	Объект.СуммаПоДокументу = Объект.Товары.Итог("Стоимость");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Если Объект.Валюта = ПредопределенноеЗначение("Справочник.Валюты.Рубль") ИЛИ НЕ ЗначениеЗаполнено(Объект.Валюта) Тогда
			ТекСтрока.Цена = ЦенаРеализацииНоменклатурыНаСервере(ТекСтрока.Номенклатура);
			ОбработкаТабличнойЧастиКлиент.РассчитатьСуммуВСтроке(ТекСтрока);
			Объект.СуммаПоДокументу = Объект.Товары.Итог("Стоимость");
		КонецЕсли;  
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Автор) Тогда
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодбор(Команда)
	
	ПарамерыФормы = Новый Структура;
	ПарамерыФормы.Вставить("СкладПараметр", Объект.Склад);
	ПарамерыФормы.Вставить("ВидЦеныПараметр", Объект.ВидЦены);
	ПарамерыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПарамерыФормы.Вставить("МножественныйВыбор", Истина);
	ПарамерыФормы.Вставить("АдресВХ", ПоместитьТЧТоварыВВременноеХранилище());
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаПодбора", ПарамерыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТЧТоварыВВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить());
	
КонецФункции 

&НаКлиенте
Процедура ОбработатьПодбор(АдресВХ) Экспорт

	ОбработатьПодборНаСервере(АдресВХ);
	Объект.СуммаПоДокументу = Объект.Товары.Итог("Стоимость");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПодборНаСервере(АдресВХ) 

	Объект.Товары.Загрузить(ПолучитьИзВременногоХранилища(АдресВХ));
	
КонецПроцедуры

