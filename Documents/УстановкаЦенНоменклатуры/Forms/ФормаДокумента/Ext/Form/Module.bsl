
&НаСервере
Процедура ЗаполнитьНоменклатуруНаСервере()
	
	Если НЕ Объект.ВидЦены.Пустая() Тогда
		Объект.Товары.Очистить();	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураСпр.Ссылка КАК Номенклатура,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследнихСЦеной.Цена, 0) КАК СтараяЦена
		|ПОМЕСТИТЬ ВТ_НоменклатураСНулевойЦенойНаДату
		|ИЗ
		|	Справочник.Номенклатура КАК НоменклатураСпр
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				,
		|				Период = &Дата
		|					И ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО (ЦеныНоменклатурыСрезПоследних.Номенклатура = НоменклатураСпр.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследнихСЦеной
		|		ПО (ЦеныНоменклатурыСрезПоследнихСЦеной.Номенклатура = НоменклатураСпр.Ссылка)
		|ГДЕ
		|	НЕ НоменклатураСпр.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
		|	ВТ_Номенклатура.Цена КАК Цена,
		|	ВТ_Номенклатура.СтараяЦена КАК СтараяЦена
		|ИЗ
		|	ВТ_НоменклатураСНулевойЦенойНаДату КАК ВТ_Номенклатура
		|ГДЕ
		|	ВТ_Номенклатура.Цена = 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура";
		
		Запрос.УстановитьПараметр("Дата", НачалоДня(Объект.Дата));
		Запрос.УстановитьПараметр("ВидЦены", Объект.ВидЦены);
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = Объект.Товары.Добавить();
			НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			НоваяСтрока.СтараяЦена = ВыборкаДетальныеЗаписи.СтараяЦена;
		КонецЦикла;
	Иначе
		Сообщить("Вид цены не установлен!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНоменклатуруНаСервере(); 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Автор) Тогда
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;	
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		Элементы.ВидЦены.Вид = ВидПоляФормы.ПолеНадписи;
		Элементы.ВидЦены.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицу(Команда) 
	
	Объект.Товары.Очистить();
	
КонецПроцедуры
